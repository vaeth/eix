#compdef eix-remote
local a
a=(
  'update'{,1}'\:"Fetch eix-caches1 and add them to the eix database"'
  'update2\:"Fetch eix-caches2 and add them to the eix database"'
  'add'{,1}'\:"Add fetched eix-caches1 to the eix database"'
  'add2\:"Add fetched eix-caches2 to the eix database"'
  'fetch'{,1}'\:"Fetch eix-caches1"'
  'fetch2\:"Fetch eix-caches2"'
  'remove\:"remove temporarily added virtual overlays from eix database"'
)
_arguments -s -S -A '-*' : \
'(1 -)'{'-h','-\\?'}'[help]' \
'(1 -)-L[print LOCAL_LAYMAN and exit]' \
'-k[keep permissions]' \
'-K[keep permissions, resistent to -i]' \
'(-X)-x[exclude local layman overlays which are remote]' \
'(-x)-X[exclude remote overlays which are local layman]' \
'-l+[PATH (use PATH as LOCAL_LAYMAN)]:LOCAL_LAYMAN:_files -/' \
'-a+[ADDR (use ADDR as remote database)]:remote address: ' \
'-i[ignore all previous options]' \
'-v[verbose (default)]' \
'-H[no status line update]' \
'-q[quiet]' \
"1:command:(($a))"
